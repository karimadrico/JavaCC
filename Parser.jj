options {
  STATIC = false;
}

PARSER_BEGIN(CobolCompiler)
import java.util.*;
import java.io.*;

public class CobolCompiler {

  private List<String> codigoIntermedio = new ArrayList<>();
  private int contadorEtiquetas = 0;

  private String nuevaEtiqueta() {
    return "LBL" + (contadorEtiquetas++);
  }

  private void agregarCodigo(String c) {
    codigoIntermedio.add("    " + c);
  }

  static class BooleanResult {
    String e1, e2, op;
    boolean negado;

    BooleanResult(String e1, String e2, String op, boolean n) {
      this.e1 = e1;
      this.e2 = e2;
      this.op = op;
      this.negado = n;
    }
  }

  private void generarSalto(BooleanResult br, String etiquetaFalsa) {
    String op = br.op;

    if (br.negado) {
      if (op.equals(">")) op = "<=";
      else if (op.equals("<")) op = ">=";
      else if (op.equals("==")) op = "!=";
    }

    agregarCodigo("if " + br.e1 + " " + op + " " + br.e2 + " goto " + etiquetaFalsa);
  }

  public static void main(String[] args) throws Exception {
    InputStream in = (args.length > 0) ? new FileInputStream(args[0]) : System.in;
    CobolCompiler p = new CobolCompiler(in);
    p.programa();
    for (String s : p.codigoIntermedio) System.out.println(s);
    System.out.println("Program parsed successfully.");
  }
}
PARSER_END(CobolCompiler)

/* ===================== SKIP ===================== */

SKIP : { " " | "\t" | "\r" | "\n" }

SKIP : {
  < COMMENT_LINE : "\n" " " " " " " " " " " " " "*" (~["\n","\r"])* >
}

SKIP : {
  < COMMENT : "*>"
    (~["\n","\r"])* >
}

/* ===================== TOKENS ===================== */

TOKEN : { < DOT: "." > }

TOKEN : { < PROGRAM: "PROGRAM" > }
TOKEN : { < BEGIN: "BEGIN" > }
TOKEN : { < END: "END" > }
TOKEN : { < IF: "IF" > }
TOKEN : { < THEN: "THEN" > }
TOKEN : { < ELSE: "ELSE" > }
TOKEN : { < WHILE: "WHILE" > }
TOKEN : { < DO: "DO" > }
TOKEN : { < VARYING: "VARYING" > }
TOKEN : { < FROM: "FROM" > }
TOKEN : { < TO: "TO" > }
TOKEN : { < BY: "BY" > }
TOKEN : { < IS: "IS" > }
TOKEN : { < NOT: "NOT" > }
TOKEN : { < GREATER: "GREATER" > }
TOKEN : { < LESS: "LESS" > }
TOKEN : { < EQUAL: "EQUAL" > }
TOKEN : { < THAN: "THAN" > }

TOKEN : {
  < ID: (["A"-"Z"])(["A"-"Z","0"-"9","-"])* >
| < NUM: (["0"-"9"])+ >
}

/* ===================== GRAM√ÅTICA ===================== */

void programa() : {} {
  <PROGRAM> <ID> <DOT>
  <BEGIN>
  sentencias()
  <END> <DOT>
}

void sentencias() : {} {
  ( sentencia() )*
}

void sentencia() : {} {
  ( condicional() <DOT>
  | bucle() <DOT>
  )
}

void condicional() : {
  BooleanResult br;
  String lblElse, lblFin;
} {
  <IF> br=booleanExpr() <THEN>
  {
    lblElse = nuevaEtiqueta();
    lblFin = nuevaEtiqueta();
    generarSalto(br, lblElse);
  }
  sentencias()
  {
    agregarCodigo("goto " + lblFin);
    agregarCodigo(lblElse + ":");
  }
  (
    <ELSE> sentencias()
    |
    {}
  )
  {
    agregarCodigo(lblFin + ":");
  }
  <END>
}

void bucle() : {
  BooleanResult br;
  String lblIni, lblFin;
  Token id;
  String fin;
} {
  (
    <WHILE>
    {
      lblIni = nuevaEtiqueta();
      lblFin = nuevaEtiqueta();
      agregarCodigo(lblIni + ":");
    }
    br=booleanExpr() <DO>
    {
      generarSalto(br, lblFin);
    }
    sentencias() <END>
    {
      agregarCodigo("goto " + lblIni);
      agregarCodigo(lblFin + ":");
    }
    |
    <VARYING> id=<ID> <TO> fin=valor() <DO>
    {
      lblIni = nuevaEtiqueta();
      lblFin = nuevaEtiqueta();
      agregarCodigo(id.image + " := 1");
      agregarCodigo(lblIni + ":");
      agregarCodigo("if " + id.image + " > " + fin + " goto " + lblFin);
    }
    sentencias() <END>
    {
      agregarCodigo(id.image + " := " + id.image + " + 1");
      agregarCodigo("goto " + lblIni);
      agregarCodigo(lblFin + ":");
    }
  )
}

String valor() : { Token t; } {
  ( t=<ID> | t=<NUM> )
  { return t.image; }
}

BooleanResult booleanExpr() : {
  String e1, e2, op;
  boolean neg = false;
} {
  e1=valor() <IS>
  (
    <NOT> { neg = true; }
    ( <GREATER> <THAN> { op = ">"; }
    | <LESS> <THAN> { op = "<"; }
    | <EQUAL> <TO> { op = "=="; } )
    |
    <GREATER> <THAN> { op = ">"; }
    | <LESS> <THAN> { op = "<"; }
    | <EQUAL> <TO> { op = "=="; }
  )
  e2=valor()
  {
    return new BooleanResult(e1, e2, op, neg);
  }
}
