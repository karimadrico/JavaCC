options {
  STATIC = false;
}

PARSER_BEGIN(CobolCompiler)

import java.util.*;
import java.io.*;

public class CobolCompiler {

  /* Variables usadas en el programa */
  private Set<String> variables = new HashSet<>();

  /* Código intermedio generado */
  private List<String> codigo = new ArrayList<>();

  /* Contadores para temporales y etiquetas */
  private int tempCount = 1;
  private int labelCount = 0;

  private String nuevaTemp() {
    return "t" + (tempCount++);
  }

  private String nuevaEtiqueta() {
    return "L" + (labelCount++);
  }

  private void emit(String s) {
    codigo.add("    " + s);
  }

  /* Resultado de una expresión booleana */
  static class BooleanResult {
    String e1, e2, op;
    boolean negated;

    BooleanResult(String e1, String e2, String op, boolean neg) {
      this.e1 = e1;
      this.e2 = e2;
      this.op = op;
      this.negated = neg;
    }
  }

  /* Salto cuando la condición es falsa */
  private void saltoFalso(BooleanResult br, String etiquetaFalsa) {
    String op = br.op;

    if (br.negated) {
      if (op.equals(">")) op = "<=";
      else if (op.equals("<")) op = ">=";
      else if (op.equals("==")) op = "!=";
    }

    String opNeg;
    if (op.equals(">")) opNeg = "<=";
    else if (op.equals("<")) opNeg = ">=";
    else if (op.equals("==")) opNeg = "!=";
    else if (op.equals("!=")) opNeg = "==";
    else opNeg = op;

    emit("if " + br.e1 + " " + opNeg + " " + br.e2 + " goto " + etiquetaFalsa);
  }

  public static void main(String[] args) throws Exception {
    InputStream in = (args.length > 0) ? new FileInputStream(args[0]) : System.in;
    CobolCompiler parser = new CobolCompiler(in);
    parser.program();
    for (String s : parser.codigo) {
      System.out.println(s);
    }
  }
}

PARSER_END(CobolCompiler)

/* ================== SKIP ================== */

/* Comentarios COBOL: asterisco en columna 7 */
SKIP :
{
  ("\n" | "\r\n")
  " " " " " " " " " "
  "*"
  (~["\n","\r"])*
}

/* Comentarios *> */
SKIP :
{
  "*>"
  (~["\n","\r"])*
}

/* Espacios en blanco */
SKIP :
{
  " " | "\t" | "\n" | "\r"
}

/* ================== TOKENS ================== */

TOKEN : { < DOT: "." > }

TOKEN : { < PROGRAM: "PROGRAM" > }
TOKEN : { < BEGIN: "BEGIN" > }
TOKEN : { < END: "END" > }

TOKEN : { < IF: "IF" > }
TOKEN : { < THEN: "THEN" > }
TOKEN : { < ELSE: "ELSE" > }

TOKEN : { < WHILE: "WHILE" > }
TOKEN : { < DO: "DO" > }

TOKEN : { < VARYING: "VARYING" > }
TOKEN : { < FROM: "FROM" > }
TOKEN : { < TO: "TO" > }
TOKEN : { < BY: "BY" > }

TOKEN : { < IS: "IS" > }
TOKEN : { < NOT: "NOT" > }
TOKEN : { < GREATER: "GREATER" > }
TOKEN : { < LESS: "LESS" > }
TOKEN : { < EQUAL: "EQUAL" > }
TOKEN : { < THAN: "THAN" > }

TOKEN : { < MOVE: "MOVE" > }
TOKEN : { < ACCEPT: "ACCEPT" > }
TOKEN : { < DISPLAY: "DISPLAY" > }

TOKEN : { < PA: "(" > }
TOKEN : { < PC: ")" > }

TOKEN :
{
  < ID: (["A"-"Z"])(["A"-"Z","0"-"9","-"])* >
| < NUM: (["0"-"9"])+ >
| < CAD: "\"" (~["\""])* "\"" | "'" (~["'"])* "'" >
}

/* ================== GRAMÁTICA ================== */

void program() : {}
{
  <PROGRAM> <ID> <DOT>
  <BEGIN>
  stmts()
  <END> <DOT>
}

void stmts() : {}
{
  ( stmt() )*
}

void stmt() : {}
{
    loop() <DOT>
  | cond() <DOT>
  | assig() <DOT>
  | io() <DOT>
}

/* --------- BUCLES (Punto 4 y 5) --------- */

void loop() :
{
  BooleanResult br;
  String lIni, lFin;
  Token id;
  String ini, fin, paso;
}
{
  <WHILE>
  {
    lIni = nuevaEtiqueta();
    lFin = nuevaEtiqueta();
    emit(lIni + ":");
  }
  br=booleanExpr() <DO>
  {
    saltoFalso(br, lFin);
  }
  stmts()
  <END>
  {
    emit("goto " + lIni);
    emit(lFin + ":");
  }

|
  <VARYING> id=<ID>
  {
    lIni = nuevaEtiqueta();
    lFin = nuevaEtiqueta();
  }
  (
    <FROM> ini=atomic()
    | { ini = "1"; }
  )
  <TO> fin=atomic()
  (
    <BY> paso=atomic()
    | { paso = "1"; }
  )
  <DO>
  {
    emit(id.image + " := " + ini);
    emit(lIni + ":");
    emit("if " + id.image + " > " + fin + " goto " + lFin);
  }
  stmts()
  <END>
  {
    emit(id.image + " := " + id.image + " + " + paso);
    emit("goto " + lIni);
    emit(lFin + ":");
  }
}

/* --------- CONDICIONAL (Punto 3) --------- */

void cond() :
{
  BooleanResult br;
  String lElse, lFin;
}
{
  <IF> br=booleanExpr() <THEN>
  {
    lElse = nuevaEtiqueta();
    lFin = nuevaEtiqueta();
    saltoFalso(br, lElse);
  }
  stmts()
  {
    emit("goto " + lFin);
    emit(lElse + ":");
  }
  ( <ELSE> stmts() )?
  <END>
  {
    emit(lFin + ":");
  }
}

/* --------- ASIGNACIÓN e IO --------- */

void assig() : {} { <MOVE> expr() <TO> <ID> }

void io() : {} { <DISPLAY> literal() | <ACCEPT> <ID> }

void literal() : {} { <ID> | <NUM> | <CAD> }

/* --------- EXPRESIONES --------- */

String atomic() :
{
  Token t;
}
{
  ( t=<ID> | t=<NUM> )
  { return t.image; }
}

String expr() :
{
  String s;
}
{
  s=mult() { return s; }
}

String mult() :
{
  Token t;
  String e;
}
{
    t=<ID>  { return t.image; }
  | t=<NUM> { return t.image; }
  | <PA> e=expr() <PC> { return e; }
}

/* --------- BOOLEANEXPR (Punto 2) --------- */

BooleanResult booleanExpr() :
{
  String e1, e2, op;
  boolean neg = false;
}
{
  e1=expr() <IS>
  ( <NOT> { neg = true; } )?
  (
    <GREATER> <THAN> { op = ">"; }
  | <LESS> <THAN>    { op = "<"; }
  | <EQUAL> <TO>     { op = "=="; }
  )
  e2=expr()
  {
    return new BooleanResult(e1, e2, op, neg);
  }
}
