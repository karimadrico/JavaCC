options {
  STATIC = false;
}

PARSER_BEGIN(CobolCompiler)
import java.util.*;
import java.io.*;

public class CobolCompiler {

  private List<String> codigo = new ArrayList<>();
  private int etq = 0;

  private String nuevaEtiqueta() {
    return "L" + (etq++);
  }

  private void emit(String s) {
    codigo.add("    " + s);
  }

  /*  ----------------------------------- resultado estructurado de booleanExpr -----------------------------------  */
  static class BooleanResult {
    String e1;
    String e2;
    String op;
    boolean neg;

    BooleanResult(String a, String b, String o, boolean n) {
      e1 = a;
      e2 = b;
      op = o;
      neg = n;
    }
  }

  /*  ----------------------------------- Utilidad para IF y WHILE     -----------------------------------  */
  private void saltoFalso(BooleanResult br, String lblFalse) {
    String op = br.op;

    if (br.neg) {
      if (op.equals(">")) op = "<=";
      else if (op.equals("<")) op = ">=";
      else if (op.equals("==")) op = "!=";
    }

    emit("if " + br.e1 + " " + op + " " + br.e2 + " goto " + lblFalse);
  }

  public static void main(String[] args) throws Exception {
    InputStream in = (args.length > 0)
        ? new FileInputStream(args[0])
        : System.in;

    CobolCompiler p = new CobolCompiler(in);
    p.program();

    for (String s : p.codigo)
      System.out.println(s);
  }
}
PARSER_END(CobolCompiler)

/*  -----------------------------------  ESPACIOS Y COMENTARIOS  -----------------------------------  */

SKIP : { " " | "\t" | "\r" | "\n" }

SKIP : {
  < LINE_COMMENT :
    "\n" " " " " " " " " " " " "*" (~["\n","\r"])* >
}

SKIP : {
  < INLINE_COMMENT : "*>"
    (~["\n","\r"])* >
}

/* ===================== TOKENS ===================== */

TOKEN : { < DOT: "." > }

TOKEN : { < PROGRAM: "PROGRAM" > }
TOKEN : { < BEGIN: "BEGIN" > }
TOKEN : { < END: "END" > }
TOKEN : { < IF: "IF" > }
TOKEN : { < THEN: "THEN" > }
TOKEN : { < ELSE: "ELSE" > }
TOKEN : { < WHILE: "WHILE" > }
TOKEN : { < DO: "DO" > }
TOKEN : { < IS: "IS" > }
TOKEN : { < NOT: "NOT" > }
TOKEN : { < GREATER: "GREATER" > }
TOKEN : { < LESS: "LESS" > }
TOKEN : { < EQUAL: "EQUAL" > }
TOKEN : { < THAN: "THAN" > }
TOKEN : { < TO: "TO" > }

TOKEN : {
  < ID: (["A"-"Z"])(["A"-"Z","0"-"9","-"])* >
| < NUM: (["0"-"9"])+ >
}

/* ===================== GRAM√ÅTICA ===================== */

void program() : {} {
  <PROGRAM> <ID> <DOT>
  <BEGIN>
  stmts()
  <END> <DOT>
}

void stmts() : {} {
  ( stmt() )*
}

void stmt() : {} {
  ( cond() <DOT>
  | loop() <DOT>
  )
}

/* ===================== COND ===================== */

void cond() : {
  BooleanResult br;
  String lblElse, lblEnd;
} {
  <IF> br=booleanExpr() <THEN>
  {
    lblElse = nuevaEtiqueta();
    lblEnd  = nuevaEtiqueta();
    saltoFalso(br, lblElse);
  }
  stmts()
  {
    emit("goto " + lblEnd);
    emit(lblElse + ":");
  }
  (
    <ELSE> stmts()
    | {}
  )
  {
    emit(lblEnd + ":");
  }
  <END>
}

/* ===================== LOOP / WHILE  ===================== */

void loop() : {
  BooleanResult br;
  String lblIni, lblFin;
} {
  <WHILE>
  {
    lblIni = nuevaEtiqueta();
    lblFin = nuevaEtiqueta();
    emit(lblIni + ":");
  }
  br=booleanExpr() <DO>
  {
    saltoFalso(br, lblFin);
  }
  stmts()
  <END>
  {
    emit("goto " + lblIni);
    emit(lblFin + ":");
  }
}

/* ===================== VALORES ===================== */

String atomic() : {
  Token t;
} {
  ( t=<ID> | t=<NUM> )
  { return t.image; }
}

/* ===================== BOOLEANEXPR ===================== */

BooleanResult booleanExpr() : {
  String e1, e2, op;
  boolean neg = false;
} {
  e1=atomic() <IS>
  (
    <NOT> { neg = true; }
    (
      <GREATER> <THAN> { op = ">"; }
      | <LESS> <THAN> { op = "<"; }
      | <EQUAL> <TO> { op = "=="; }
    )
    |
    <GREATER> <THAN> { op = ">"; }
    | <LESS> <THAN> { op = "<"; }
    | <EQUAL> <TO> { op = "=="; }
  )
  e2=atomic()
  {
    return new BooleanResult(e1, e2, op, neg);
  }
}
